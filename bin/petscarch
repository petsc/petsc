#! /bin/csh -f
#
#  Returns the arch of the machine
#
if ($?ARCH) then
    set LARCH = $ARCH
else if (-e /bin/arch) then
    set LARCH = `/bin/arch`
else if (-e /bin/uname) then
    set LARCH = `/bin/uname -s`
    if ("$LARCH" == "AIX") then
        set LARCH = "rs6000"
    else if ("$LARCH" == "ULTRIX") then
        set LARCH = `/bin/uname -m`
        if ("$LARCH" == "RISC") then
            set LARCH = "dec5000"
        endif
    else if ("$LARCH" == "HP-UX") then
	set LARCH = "hpux"
    else if ("$LARCH" != "IRIX") then 
        set LARCH = `/bin/uname -m`
    endif
else if (-e /usr/local/bin/arch) then
    set LARCH = `/usr/local/bin/arch`
else
    set LARCH = "unknown"
endif
set SunOSTest = `expr "$LARCH" : "\(....\)"`
if ("$SunOSTest" == "sun4") then
  set LARCH = sun4
  set Version = `/bin/uname -r`
  # In "improving" SunOS, the useful feature of "substr" was withdrawn 
  # from expr.  Can't let the users have life too easy, can we?  This 
  # means that we can't just use 
  #   set MajorVersion = `expr substr $Version 1 1`
  # because it won't work on Solaris systems.  The following should work on
  # both:
  set MajorVersion = `expr "$Version" : "\(.\)"`
  if ("$MajorVersion" ==  5) then
    set LARCH = "solaris"
  endif
else if ("$LARCH" == "AIX") then
   set LARCH = "rs6000"
else if ("$LARCH" == "RIOS") then
   set LARCH = "rs6000"
else if ("$LARCH" == "mips") then
   set LARCH = "dec5000"
else if ("$LARCH" == "dec-5000") then
   set LARCH = "dec5000"
else if ("$LARCH" == "IP12") then
  set LARCH = "IRIX"
else if ("$LARCH" == "i386") then
  set LARCH = "ipsc2"
else if ("$LARCH" == "CRAY C90") then
  set LARCH = "cray"
else if ("$LARCH" == "CRAY-2") then
  set LARCH = "cray"
else if ("$LARCH" == "sun4m") then
  set LARCH = "sun4"
else if ("$LARCH" == "iris4d") then
  set LARCH = "IRIX"
else if ("$LARCH" == "next") then
  set LARCH = "NeXT"
endif
#
echo $LARCH
exit 0
